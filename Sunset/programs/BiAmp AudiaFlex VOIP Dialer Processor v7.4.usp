/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name: Crestron Electronics
System Name:
System Number:
Programmer:	PAC
Comments:
*/
/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
#SYMBOL_NAME "BiAmp AudiaFlex VoIP Dialer Processor v7.4"
// #HINT ""
#DEFINE_CONSTANT	OFFTOON			110
#DEFINE_CONSTANT	ONTOOFF			260
#DEFINE_CONSTANT	MAXPRESETS		16
#DEFINE_CONSTANT	ciMAXENTRIES	16

#DEFINE_CONSTANT	csSENDNAME		"Send Name\x0D\x0A"

#CATEGORY	"0"
#DEFAULT_VOLATILE
// #DEFAULT_NONVOLATILE
// #ENABLE_TRACE

#HELP_BEGIN
	Only to be used in conjunction with the BiAmp Audia Flex VOIP Dialer Module v7.4
#HELP_END

DIGITAL_INPUT	On_Hook, Off_Hook, KeyboardGo, Clear, Back, Dial, Redial, Clear_Last#_Dialed, 
				Preset_Store, Get_Names, First, Scroll_Up, Scroll_Down, Poll, Answer_Incoming_Call,
				Reject_Incoming_Call, Hold, Resume, Dial_Entry[ciMAXENTRIES],
				Presets[MAXPRESETS];
DIGITAL_OUTPUT	On_Hook_FB, Off_Hook_FB, Start_Up_Out, Incoming_Call; 
ANALOG_INPUT	MaxCharacters, KeyboardAn, aPreset, Entries_Per_Page, Line_Number;
STRING_INPUT	Header$[50], Address$[5], Instance$[50],phone_no_to_dial$[20];
BUFFER_INPUT	From_Smpl$[100];
STRING_OUTPUT	Phone_Number$, Caller_ID_Name_Text, Caller_ID_Number_Text, To_Device, Entry_Name$[ciMAXENTRIES];

INTEGER		iMaxChars, iPreset, Semaphore, iHook, iDisplayEntryNumber[ciMAXENTRIES], iEntriesPerPage,
			iOffset, iLine, iIncoming, iMarker1, iMarker2, iMarker3, iMarker4, iMarker5, iMarker6,
			iMarker7, iMarker8, iCounter1;
STRING		TempDial$[100], sHeader[50], Num$[50], sNUM2[10], Last_Num_Dialed$[50], Preset_Ram[MAXPRESETS][40],
			sEntryName[ciMAXENTRIES][40], sLabelCompare$[40], sHookStateCompare$[40], sInstance[52],
			sTempName[50], sTempNumber[50], sCallerIdCompare$[40];

Function fIncomingCallTimeOut()
{
	wait(1000, wIncomingCallTimeOut)
	{
		if(iIncoming = 1)
		{
			iIncoming = 0;
			Incoming_Call = iIncoming;
			sTempNumber = "";
			sTempName = "";
			Caller_ID_Number_Text = sTempNumber;
			Caller_ID_Name_Text = sTempName;
		}
	}
}

PUSH On_Hook
{
	makestring(To_Device, "SET %s VOIPHOOKSTATE %s %u 1\x0A", Address$, sInstance, iLine);
	delay(OFFTOON);
	makestring(To_Device, "GETD %s VOIPHOOKSTATE %s %u\x0A", Address$, sInstance, iLine);
	makestring(sHookStateCompare$, "#GETD %s VOIPHOOKSTATE %s %u\x20", Address$, sInstance, iLine);
}

PUSH Off_Hook
{
	makestring(To_Device, "SET %s VOIPHOOKSTATE %s %u 0\x0A", Address$, sInstance, iLine);
	delay(ONTOOFF);
	makestring(To_Device, "GETD %s VOIPHOOKSTATE %s %u\x0A", Address$, sInstance, iLine);
	makestring(sHookStateCompare$, "#GETD %s VOIPHOOKSTATE %s %u\x20", Address$, sInstance, iLine);
}

PUSH Clear_Last#_Dialed
{
	ClearBuffer(Last_Num_Dialed$);
}

PUSH Dial
{
	if((len(TempDial$) > 0) && (iHook = 1))
	{
		makestring(To_Device, "%s%s#\x0A", sHeader, TempDial$);
		Last_Num_Dialed$ = TempDial$;
		delay(ONTOOFF);
		makestring(To_Device, "GETD %s VOIPHOOKSTATE %s %u\x0A", Address$, sInstance, iLine);
		makestring(sHookStateCompare$, "#GETD %s VOIPHOOKSTATE %s %u\x20", Address$, sInstance, iLine);
	}
}
	
PUSH ReDial
{
	if((len(Last_Num_Dialed$) > 0) && (iHook = 1))
	{
		makestring(To_Device, "%s%s#\x0A", sHeader, Last_Num_Dialed$);
		Phone_Number$ = Last_Num_Dialed$;
		delay(ONTOOFF);
		makestring(To_Device,  "GETD %s VOIPHOOKSTATE %s %u\x0A", Address$, sInstance, iLine);
		makestring(sHookStateCompare$, "#GETD %s VOIPHOOKSTATE %s %u\x20", Address$, sInstance, iLine);
	}
}

CHANGE phone_no_to_dial$
{
    TempDial$=phone_no_to_dial$;
    Phone_Number$=TempDial$;
}


CHANGE Header$
{
	makestring(sHeader,"%s%d\x20", header$, iLine);trace("sHeader = %s", sHeader);
}

CHANGE Line_Number
{
	iLine = Line_Number;
}

PUSH KeyboardGo
{
	if(iHook = 0)
	{
		if(len(TempDial$) < MaxCharacters)
		{
			TempDial$ = TempDial$ + chr(KeyboardAn);
			Phone_Number$ = TempDial$;
			Last_Num_Dialed$ = TempDial$;
			makestring(To_Device,  "%s%s\x0A", sHeader, chr(KeyBoardAn));
		}
		else if(len(TempDial$) >= MaxCharacters)
		{
			TempDial$ = right(TempDial$, MaxCharacters - 1);
			TempDial$ = TempDial$ + chr(KeyboardAn);
			Phone_Number$ = TempDial$;
			Last_Num_Dialed$ = TempDial$;
			makestring(To_Device,  "%s%s\x0A", sHeader, chr(KeyBoardAn));
		}
	}
	else if(iHook = 1) 
	{
		if(len(TempDial$) < MaxCharacters)
		{
			TempDial$ = TempDial$ + chr(KeyboardAn);
			Phone_Number$ = TempDial$;
		}
		else if(len(TempDial$) >= MaxCharacters)
 		{
			TempDial$ = right(TempDial$, MaxCharacters - 1);
			TempDial$ = TempDial$ + chr(KeyboardAn);
			Phone_Number$ = TempDial$;
		}
	}
}

PUSH Clear
{
	ClearBuffer(TempDial$);
	Phone_number$ = "";
}
    
PUSH Back
{
	if(len(TempDial$) > 0)
	{
		TempDial$ = left(TempDial$, len(TempDial$) - 1);
		Phone_Number$ = TempDial$;
	}
}
	
Push Presets
{
	integer Index;
	Index = getlastmodifiedarrayindex();
	if(iHook = 1)
	{
		if(len(Preset_Ram[Index]) > 1)
		{
			Phone_Number$ = Preset_Ram[Index];
			makestring(To_Device, "%s%s#\x0A", sHeader, Preset_Ram[Index]);
			wait(300)
			{
				makestring(To_Device, "GETD %s VOIPHOOKSTATE %s %u\x0A", Address$, sInstance, iLine);		
				makestring(sHookStateCompare$, "#GETD %s VOIPHOOKSTATE %s %u\x20", Address$, sInstance, iLine);
			}
		}
	}
}

Push Preset_Store
{
	Preset_Ram[aPreset] = TempDial$;
}

Push Get_Names
{
	Integer a;
	for(a = 1 to ciMAXENTRIES)
	{
		makestring(To_Device, "GETD %s VOIPSDLABEL %s %u %u\x0A", Address$, sInstance, iLine, a);
		makestring(sLabelCompare$, "#GETD %s VOPIPSDLABEL %s %u\x20", Address$, sInstance, iLine);
	}
}

Push First
{
	Integer iO;
	iOffset = 0;
	for(iO = (iOffset + 1) to (iOffset + iEntriesPerPage))
	{
		iDisplayEntryNumber[iO] = iO;
		Entry_Name$[iO] = sEntryName[iO];
	}
}

Push Scroll_Up
{
	Integer iO;
	if(iOffset > 0)
	{
		iOffset = iOffset - 1;
		for(iO = (iOffset + 1) to (iOffset + iEntriesPerPage))
		{
			iDisplayEntryNumber[iO] = iO;
			Entry_Name$[iO] = sEntryName[iO];
		}
	}
}

Push Scroll_Down
{
	Integer iO;
	iOffset = iOffset + 1;
	if(iOffset <= (ciMAXENTRIES - iEntriesPerPage))
	{
		for(iO = (iOffset + 1) to (iOffset + iEntriesPerPage))
		{
			iDisplayEntryNumber[iO] = iO;
			Entry_Name$[iO] = sEntryName[iO];
		}
	}
	else
	{
		iOffset = iOffset - 1;
	}
}

PUSH Answer_Incoming_Call
{
	if(iIncoming = 1)
	{
		makestring(To_Device, "ANS %s VOIPCALL %s %u\x0A", Address$, sInstance, iLine);
		cancelwait(wIncomingCallTimeOut);
		iIncoming = 0;
		Incoming_Call = iIncoming;
	}
}

PUSH Reject_Incoming_Call
{
	if(iIncoming = 1)
	{
		makestring(To_Device, "END %s VOIPCALL %s %u\x0A", Address$, sInstance, iLine);
		cancelwait(wIncomingCallTimeOut);
		iIncoming = 0;
		Incoming_Call = iIncoming;
		sTempNumber = "";
		sTempName = "";
		Caller_ID_Number_Text = sTempNumber;
		Caller_ID_Name_Text = sTempName;
	}
}

PUSH Hold
{
	if(iHook = 0)
	{
		makestring(To_Device, "HOLD %s VOIPCALL %s %u\x0A", Address$, sInstance, iLine);
	}
}

PUSH Resume
{
	if(iHook = 0)
	{
		makestring(To_Device, "RESUME %s VOIPCALL %s %u\x0A", Address$, sInstance, iLine);
	}
}

Push Poll
{
	makestring(To_Device, "GETD %s VOIPHOOKSTATE %s %u\x0A", Address$, sInstance, iLine);		
	makestring(sHookStateCompare$, "#GETD %s VOIPHOOKSTATE %s %u\x20", Address$, sInstance, iLine);
}

Push Dial_Entry
{
	Integer iTemp;
	iTemp = getlastmodifiedarrayindex();
	makestring(To_Device, "DIAL %s VOIPSPEEDDIAL %s %u %u\x0A", Address$, sInstance, iLine, iDisplayEntryNumber[iTemp + iOffset]);
	wait(300)
	{
		makestring(To_Device, "GETD %s VOIPHOOKSTATE %s %u\x0A", Address$, sInstance, iLine);
		makestring(sHookStateCompare$, "#GETD %s VOIPHOOKSTATE %s %u\x20", Address$, sInstance, iLine);
	}
}

CHANGE Instance$
{
	if(find("\x20", Instance$) > 0)
	{
		makestring(sInstance, "\x22%s\x22", left(Instance$, 50));
	}
	else
	{
		sInstance = left(Instance$, 50);
	}
}

CHANGE From_SMPL$
{
	string Temp$[100];
	integer iA, iP, iEntry, iB;
	
	if(Semaphore = 0)
	{
		Semaphore = 1; 
		while(1)
		{
			Temp$ = gather("\n",FROM_SMPL$);
			if(find(csSENDNAME, Temp$) > 0)
			{
				makestring(To_Device, "%s=%s %u\x0D\x0A", left(Temp$, len(Temp$) - 2), sInstance, iLine);
			}
			else if(find(sHookStateCompare$, Temp$) > 0) 
			{
				iHook = atoi(mid(Temp$, len(sHookStateCompare$) + 1, len(Temp$) - len(sHookStateCompare$)));
				if (iHook = 1)
				{
					Off_Hook_FB = 0;
					On_Hook_FB = 1;
					if(iIncoming = 1)
					{
						cancelwait(wIncomingCallTimeOut);
						iIncoming = 0;
						Incoming_Call = iIncoming;
						sTempNumber = "";
						sTempName = "";
						Caller_ID_Number_Text = sTempNumber;
						Caller_ID_Name_Text = sTempName;
					}
				}
				else if (iHook = 0)
				{
					On_Hook_FB = 0;
					Off_Hook_FB = 1;
					if(iIncoming = 1)
					{
						cancelwait(wIncomingCallTimeOut);
						iIncoming = 0;
						Incoming_Call = iIncoming;
						sTempNumber = "";
						sTempName = "";
						Caller_ID_Number_Text = sTempNumber;
						Caller_ID_Name_Text = sTempName;
					}
				}
			}
			else if(find(sLabelCompare$, Temp$) > 0)
			{
				iEntry = atoi(mid(Temp$, len(sLabelCompare$), len(Temp$) - len(sLabelCompare$)));
				iA = find("\x20", Temp$, len(sLabelCompare$) + 2);
				iB = find("\x0D", Temp$);
				sEntryName[iEntry] = mid(Temp$, iA + 1, iB - iA - 1);
				if(iEntry = ciMAXENTRIES)
				{
					iOffset = 0;
					for(iP = 1 to iEntriesPerPage)
					{
						iDisplayEntryNumber[iP] = iP;
						Entry_Name$[iP] = sEntryName[iP];
					}
				}
			}
			else if(find(sCallerIDCompare$, Temp$) > 0)
			{
				iMarker1 = find("\x22", Temp$);
				iMarker2 = find("\x22", Temp$, iMarker1 + 1);
				iCounter1 = 0;
				while((byte(Temp$, iMarker2 - 1) = byte("\x5C", 1)) && (iCounter1 <= 10))
				{
					iMarker2 = find("\x22", Temp$, iMarker2 + 1);
					iCounter1 = iCounter1 + 1;
				}
				iMarker3 = find("\x22", Temp$, iMarker2 + 1);
				iCounter1 = 0;
				while((byte(Temp$, iMarker3 - 1) = byte("\x5C", 1)) && (iCounter1 <= 10))
				{
					iMarker3 = find("\x22", Temp$, iMarker3 + 1);
					iCounter1 = iCounter1 + 1;
				}
				iMarker4 = find("\x22", Temp$, iMarker3 + 1);
				iCounter1 = 0;
				while((byte(Temp$, iMarker4 - 1) = byte("\x5C", 1)) && (iCounter1 <= 10))
				{
					iMarker4 = find("\x22", Temp$, iMarker4 + 1);
					iCounter1 = iCounter1 + 1;
				}
				iMarker5 = find("\x22", Temp$, iMarker4 + 1);
				iCounter1 = 0;
				while((byte(Temp$, iMarker5 - 1) = byte("\x5C", 1)) && (iCounter1 <= 10))
				{
					iMarker5 = find("\x22", Temp$, iMarker5 + 1);
					iCounter1 = iCounter1 + 1;
				}
				iMarker6 = find("\x22", Temp$, iMarker5 + 1);
				iCounter1 = 0;
				while((byte(Temp$, iMarker6 - 1) = byte("\x5C", 1)) && (iCounter1 <= 10))
				{
					iMarker6 = find("\x22", Temp$, iMarker6 + 1);
					iCounter1 = iCounter1 + 1;
				}
				sTempNumber = mid(Temp$, iMarker3 + 1, iMarker4 - iMarker3);
				iMarker7 = find("\x22", sTempNumber);
				if((iMarker7 > 0) && (byte(Temp$, iMarker7 - 1) = byte("\x5C", 1)))
				{
					iMarker8 = find("\x22", sTempNumber, iMarker7 + 1);
				}
				if((iMarker8 > 0) && (byte(Temp$, iMarker8 - 1) = byte("\x5C", 1)))
				{
					makestring(sTempNumber, "%s%s%s", left(sTempNumber, iMarker7 - 2), mid(sTempNumber, iMarker7, (iMarker8 -  2) - iMarker7), right(sTempNumber, len(sTempNumber) - iMarker8 + 1));
				}
				sTempNumber = left(sTempNumber, len(sTempNumber) - 1);
				sTempName = mid(Temp$, iMarker5 + 1, iMarker6 - iMarker5);
				iMarker7 = find("\x22", sTempName);
				if((iMarker7 > 0) && (byte(Temp$, iMarker7 - 1) = byte("\x5C", 1)))
				{
					iMarker8 = find("\x22", sTempName, iMarker7 + 1);
				}
				if((iMarker8 > 0) && (byte(Temp$, iMarker8 - 1) = byte("\x5C", 1)))
				{
					makestring(sTempName, "%s%s%s", left(sTempName, iMarker7 - 2), mid(sTempName, iMarker7, (iMarker8 -  2) - iMarker7), right(sTempName, len(sTempName) - iMarker8 + 1));
				}
				sTempName = left(sTempName, len(sTempName) - 1);
				Caller_ID_Number_Text = sTempNumber;
				Caller_ID_Name_Text = sTempName;
			}
			else if(find("RING", Temp$) > 0)
			{
				iIncoming = 1;
				Incoming_Call = iIncoming;
				fIncomingCallTimeOut();
				delay(100);
				makestring(To_Device, "GETD %s VOIPCIDUSER %s %u\x0A", Address$, sInstance, iLine);
				makestring(sCallerIDCompare$, "#GETD %s VOIPCIDUSER %s %u\x20", Address$, sInstance, iLine);
			}
		}
		Semaphore = 0;
	}
}
	
Change Entries_Per_Page
{
	Start_Up_Out = 0;
	if(Entries_Per_Page <= ciMAXENTRIES)
	{
		iEntriesPerPage = Entries_Per_Page;
	}
	else
	{
		iEntriesPerPage = ciMAXENTRIES;
	}
}

Function Main()
{
	waitforinitializationcomplete();
	Semaphore = 0;
	clearbuffer(sHeader);
	iPreset = 0;
	Phone_Number$ = "";
	TempDial$ = "";
	iOffset = 0;
	Start_Up_Out = 1;
	iHook = 1;
	Off_Hook_FB = 0;
	On_Hook_FB = 1;
	iIncoming = 0;
	Incoming_Call = iIncoming;
	sTempNumber = "";
	sTempName = "";
	Caller_ID_Number_Text = sTempNumber;
	Caller_ID_Name_Text = sTempName;
}
